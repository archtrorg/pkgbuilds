# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kpmcore
pkgver=4.1.0
pkgrel=1
pkgdesc="Library for managing partitions"
arch=(x86_64)
url="https://www.kde.org/applications/system/kdepartitionmanager/"
license=(GPL2)
depends=(smartmontools kwidgetsaddons ki18n kauth qca)
makedepends=(extra-cmake-modules)
optdepends=('e2fsprogs: ext2/3/4 support'
	    'xfsprogs: XFS support'
	    'jfsutils: JFS support'
	    'reiserfsprogs: Reiser support'
	    'ntfs-3g: NTFS support'
            'dosfstools: FAT32 support'
            'fatresize: FAT resize support'
            'f2fs-tools: F2FS support'
            'exfat-utils: exFAT support'
            'nilfs-utils: nilfs support'
            'udftools: UDF support')
source=("https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('b545a357cc348c84e8875531fdfe40fc63d915f03581e5004c4bd547d579514b'
            'SKIP')
#validpgpkeys=(1EE5A3205904BAA2B88C0A9D24FD31940095C0E1) # Andrius Å tikonas <andrius@stikonas.eu>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
